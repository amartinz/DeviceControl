apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file(localProps['keystore'])))
            storeFile file(keyProps["store"])
            storePassword keyProps["storePass"]
            keyAlias keyProps["alias"]
            keyPassword keyProps["pass"]
        }
    }

    defaultConfig {
        applicationId 'org.namelessrom.devicecontrol'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            debuggable false
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    dexOptions {
        incremental false
        javaMaxHeapSize "2048M"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {}
}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
    // links to MPChartLib via a symlink, used in the Nameless Rom source tree, inherits support-v13
    compile project(':libs:mpandroidchart:MPChartLib')
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'com.koushikdutta.ion:ion:1.3.7'
}
