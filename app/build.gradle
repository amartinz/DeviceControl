apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'org.namelessrom.devicecontrol'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 138
        versionName '0.8.6.4'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName(versionName + '-git-' + gitRevision)
        }

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    // until all third party packages got updated
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    if (new File("modules/Vendor").exists()) {
        compile project(':modules:Vendor')
    } else {
        compile 'alexander.martinz.vendor:vendor:1.0.0@aar'
    }

    // see https://github.com/Evisceration/logger
    compile 'com.github.Evisceration:logger:1.0.5'

    compile project(':modules:ExecutionLibrary:library')
    compile project(':modules:HardwareLibrary:library')

    // see https://github.com/Evisceration/MaterialPreferences
    compile project(':modules:MaterialPreferences:library')

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:customtabs:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'

    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'

    // see https://github.com/koush/ion
    compile 'com.koushikdutta.ion:ion:2.1.6'

    // see https://www.pollfish.com/android
    compile 'com.pollfish:pollfish:4.1.0:googleplayRelease@aar'
    // switch to 8.4.0 once pollfish updates
    compile 'com.google.android.gms:play-services-ads:8.4.0'

    // see https://github.com/pilgr/Paper
    //compile 'io.paperdb:paperdb:0.9'
    compile 'com.github.Evisceration:Paper:6ad14da863'

    // see https://github.com/Kennyc1012/BottomSheet
    compile 'com.github.Kennyc1012:BottomSheet:2.0.7'

    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.1.0'
    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.1.0'
    // see https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.5.1@aar'

    // Sense360
    compile 'com.sense360:sense360-quinoa:2.0.3@aar'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
}
